#!/usr/bin/perl

use strict;
use warnings;
use 5.010;
use Getopt::Long qw( GetOptions );
use Pod::Usage qw( pod2usage );

# PODNAME: xd
# ABSTRACT: hex dump
# VERSION

=head1 SYNOPSIS

 % xd foo.jpg
 % xd --help
 % xd --version

=head1 DESCRIPTION

This command is a wee bit like od except that it doesn't have any options and dumps hex and ASCII (printable)
only.

=head1 OPTIONS

=head2 --help | -h

Print help and exit

=head2 --version

Print version and exit

=head1 SEE ALSO

=over 4

=item L<Data::HexDump>

=back

=cut

GetOptions(
  'help|h'       => sub { pod2usage({ -verbose => 2}) },
  'version'      => sub {
    say 'ACPS::Release version ', ($main::VERSION // 'dev');
    exit 1;
  },
) || pod2usage(1);

my $offset = 0;
my $buff;

if(@ARGV > 0)
{
  foreach my $fn (@ARGV)
  {
    open my $fh, '<', $fn;
    do_file($fh);
    close $fh;
  }
}
else
{
  do_file(\*STDIN);
}

sub do_file
{
  my $fh = shift;
  binmode $fh;
  while(my $bytes = read($fh, $buff, 16))
  {  
    if(defined $bytes)
    {
      my $hex = unpack('H*', $buff);
      $hex =~ s/([a-f0-9]{2})/$1 /g;
      my $str = $buff;
      $str =~ s/[^[:graph:] ]/./g;
      printf "%08x %-48s%s\n", $offset, $hex, $str;
      $offset += $bytes;
    }
    else
    {
      die "error reading file: $!";
    }
  }
  close $fh;
}
